import org.gradle.internal.os.OperatingSystem

plugins {
    alias(libs.plugins.android.library)
    id("de.undercouch.download") version "5.6.0"
    id 'maven-publish'
}

group = 'com.github.dalgarins'

android {
    namespace 'org.spatialite'
    compileSdk 34
    ndkVersion '27.3.13750724'

    defaultConfig {
        minSdk 21

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"

        externalNativeBuild {
            ndkBuild {
                arguments "-j8", "V=1"
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    publishing {
        singleVariant('release') {
            withSourcesJar()
        }
    }

    sourceSets.main.jni.srcDirs = ['src/main/none']

    externalNativeBuild {
        ndkBuild {
            path "src/main/jni/Android.mk"
        }
    }
}

ext {
    sqliteDistributionUrl = 'http://sqlite.org/2016/sqlite-amalgamation-3150100.zip'
}

tasks.register('downloadSqlite', Download) {
    src project.sqliteDistributionUrl
    dest 'src/main/jni/ndk-modules/sqlite/sqlite.zip'
}

tasks.register('installSqlite', Copy) {
    dependsOn downloadSqlite
    from zipTree(downloadSqlite.dest).matching {
        include '*/sqlite3.*'
        eachFile { it.setPath(it.getName()) }
    }
    into 'src/main/jni/ndk-modules/sqlite'
}

preBuild.dependsOn installSqlite

dependencies {

    implementation libs.appcompat
    implementation libs.material
    testImplementation libs.junit
    androidTestImplementation libs.ext.junit
    androidTestImplementation libs.espresso.core
}

publishing {
    publications {
        release(MavenPublication) {
            groupId = 'com.github.dalgarins'
            artifactId = 'android-spatialite'
            version = '2.1.2-alpha'

            afterEvaluate {
                from components.release
            }
        }
    }
}